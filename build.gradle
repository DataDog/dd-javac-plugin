import org.gradle.internal.jvm.Jvm

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}

allprojects {
    group "com.datadoghq"
    version "1.0-SNAPSHOT"

    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    java {
        withJavadocJar()
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
    }

    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }

    publishing {
        publications {
            maven(MavenPublication) { MavenPublication publication ->
                publication.from components.java
            }
        }
    }

    signing {
        useInMemoryPgpKeys(System.getenv("GPG_PRIVATE_KEY"), System.getenv("GPG_PASSWORD"))
        sign publishing.publications.maven
    }

    tasks.withType(GenerateMavenPom).configureEach {
        doFirst {
            MavenPom pom = it.pom
            pom.name = project.name
            pom.description = project.description
            pom.packaging = "jar"
            pom.url = "https://github.com/datadog/dd-javac-plugin"
            pom.licenses {
                license {
                    name = "The Apache Software License, Version 2.0"
                    url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution = "repo"
                }
            }
            pom.scm {
                connection = "scm:https://datadog@github.com/datadog/dd-javac-plugin"
                developerConnection = "scm:git@github.com:datadog/dd-javac-plugin.git"
                url = "https://github.com/datadog/dd-javac-plugin"
            }
            pom.developers {
                developer {
                    id = "datadog"
                    name = "Datadog"
                }
            }
        }
    }
}

description = "Datadog Java Compiler Plugin"

dependencies {
    implementation project(":dd-javac-plugin-client")
    implementation files(Jvm.current().toolsJar)

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"
}

gradle.taskGraph.whenReady { TaskExecutionGraph taskGraph ->
    if (taskGraph.hasTask("publishToSonatype")) {
        assert System.getenv("SONATYPE_USERNAME") != null
        assert System.getenv("SONATYPE_PASSWORD") != null

        assert System.getenv("GPG_PRIVATE_KEY") != null
        assert System.getenv("GPG_PASSWORD") != null
    }
}

nexusPublishing {
    repositories {
        if (project.hasProperty("forceLocal")) {
            local {
                // For testing use with https://hub.docker.com/r/sonatype/nexus
                // docker run --rm -d -p 8081:8081 --name nexus sonatype/nexus
                // Doesn't work for testing releases due to staging
                nexusUrl = uri("http://localhost:8081/nexus/content/repositories/releases/")
                snapshotRepositoryUrl = uri("http://localhost:8081/nexus/content/repositories/snapshots/")

                username = "admin"
                password = "admin123"

                allowInsecureProtocol = true
            }
        } else {
            sonatype {
                username = System.getenv("SONATYPE_USERNAME")
                password = System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}
